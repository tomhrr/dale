#|
@module list

Concept macro for a doubly-linked list. Apart from the `List` macro
and concept macro, the documentation in this module is for a generated
list instance of type `T`.

All of the functions that take `Iterator` arguments are defined for
`ReverseIterator`s as well, notwithstanding that there is no
documentation for those instances.

The `List` type implements the following concepts:

  * `FrontInsertionSequence`;
  * `BackInsertionSequence`; and
  * `ReversibleContainer`.

Its iterators implement `OutputIterator` and `BidirectionalIterator`.

|#
(module list (attr cto))

(import cstdio)
(import macros)
(import cstdlib)
(import math)
(import concepts)
(import algorithms)
(import utility)

#|
@macro List

Expands to the concrete type name of the `List` generated by way of
the concept macro.

@param T    The type node.
|#
(def List
  (macro extern (T)
    (let ((typename (array-of 250 char)))
      (and (not (make-type-string mc "list" T typename))
           (return T))
      (def n (var auto (p DNode) (std.macros.mnfv mc typename)))
      (return n))))

(using-namespace std.macros
(using-namespace std.concepts

#|
@concept-macro List

Expands to a `List` definition over the relevant type.

@param T    The type node.
|#
(def-concept-macro List extern ((T Type))
  (let ((typename     (array-of 250 char))
        (nodetypename (array-of 250 char))
        (etypename    (array-of 250 char)))

    ; Generate the type names and nodes, and register the main 'list'
    ; type name.
    (and (not (make-type-string mc "list" T typename))
         (return (nullptr DNode)))
    (and (not (make-type-string mc "listnode" T nodetypename))
         (return (nullptr DNode)))
    (and (not (make-type-display-string mc "List" T etypename))
         (return (nullptr DNode)))
    
    (def typenode     (var auto (p DNode) (mnfv mc typename)))
    (def nodetypenode (var auto (p DNode) (mnfv mc nodetypename)))
    
    (register-type mc typename etypename)
    
    (qq do

    (using-namespace std.macros
    (using-namespace std.concepts

    (import derivations)

    (def (uq nodetypenode)
      (struct extern
          ((element (uq T))
           (next    (p (uq nodetypenode)))
           (prev    (p (uq nodetypenode))))))

    #|
    @struct (List T)

    The core list structure type.

    @linkage extern
    |#
    (def (uq typenode)
      (struct extern 
          ((first (p (uq nodetypenode)))
           (last  (p (uq nodetypenode))))))

    #|
    @fn init

    Initialise a list.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    bool
    |#
    (def init
      (fn extern bool ((lst (ref (uq typenode))))
        (setf (:@ lst first) (nullptr (uq nodetypenode)))
        (setf (:@ lst last)  (nullptr (uq nodetypenode)))
        true))

    #|
    @fn empty

    Determine whether the list is empty.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    bool
    |#
    (def empty
      (fn extern bool ((lst (ref (const (uq typenode)))))
        (null (@:@ lst first))))

    #|
    @fn size

    Returns the number of elements in the list.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    size
    |#
    (def size
      (fn extern size ((lst (ref (const (uq typenode)))))
        (let ((n     size 0)
              (first \ (cast (@:@ lst first) (p (uq nodetypenode)))))
          (while (not (null first))
            (setv n (+ n (cast 1 size)))
            (setv first (@:@ first next)))
          n)))

    #|
    @fn max-size

    Returns the number of elements that can be accommodated by the
    list.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    size
    |#
    (def max-size
      (fn extern size ((lst (ref (const (uq typenode)))))
        (- (cast 0 size) (cast 1 size))))

    #|
    @macro value-type

    Expands to the underlying value type (i.e. `T`) of the list. This
    only uses the type node for dispatch purposes, so it's safe to
    call this with e.g. `(nullptr (List T))` as the argument.

    @param lst      A type node.
    @param-type     (p (List T))
    @linkage        extern
    |#
    (def value-type
      (macro extern ((lst (p (uq typenode))))
        (qq do (uq T))))

    #|
    @macro size-type

    Expands to the underlying size type of the list. 

    @param lst      A type node.
    @param-type     (p (List T))
    @linkage        extern
    |#
    (def size-type
      (macro extern ((lst (p (uq typenode))))
        (qq do size)))

    #|
    @macro difference-type

    Expands to the underlying iterator difference type of the list. 

    @param lst      A type node.
    @param-type     (p (List T))
    @linkage        extern
    |#
    (def difference-type
      (macro extern ((lst (p (uq typenode))))
        (qq do ptrdiff)))

    #|
    @fn front

    Returns the value of the first element in the list.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    T
    |#
    (def front
      (fn extern (uq T) ((lst (ref (const (uq typenode)))))
        (@:@ (@:@ lst first) element)))

    #|
    @fn back

    Returns the value of the last element in the list.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    T
    |#
    (def back
      (fn extern (uq T) ((lst (ref (const (uq typenode)))))
        (@:@ (@:@ lst last) element)))

    #|
    @fn push-back

    Adds an element to the end of the list.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @param value    The value to add to the list.
    @param-type     T
    @linkage        extern
    @return-type    bool
    |#
    (def push-back
      (fn extern bool ((lst (ref (uq typenode))) (value (uq T)))
        (let ((new-node \ (malloc' 1 (uq nodetypenode))))
          (setf (:@ new-node element) value)
          (setf (:@ new-node next) (nullptr (uq nodetypenode)))
          (setf (:@ new-node prev) (nullptr (uq nodetypenode)))
          (and (null (@:@ lst first))
               (do (setf (:@ lst first) new-node)
                   (setf (:@ lst last)  new-node)
                   (return true)))
          (let ((last-node \ (@:@ lst last)))
            (setf (:@ last-node next) new-node)
            (setf (:@ new-node  prev) last-node)
            (setf (:@ lst last) new-node)))
        true))

    #|
    @fn pop-back

    Removes an element from the end of the list.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    void
    |#
    (def pop-back
      (fn extern void ((lst (ref (uq typenode))))
        (and (empty (@ lst)) (return))
        (let ((last-node        \ (@:@ lst last))
              (second-last-node \ (@:@ last-node prev))
              (size             \ (size (@ lst))))
          (if (null second-last-node)
              (do (setf (:@ lst first) (nullptr (uq nodetypenode)))
                  (setf (:@ lst last)  (nullptr (uq nodetypenode))))
              (do (setf (:@ second-last-node next)
                        (nullptr (uq nodetypenode)))
                  (setf (:@ lst last) second-last-node)))
          (let ((value \ (:@ last-node element)))
            (destroy value))
            (free' last-node))
        (return)))

    #|
    @fn push-front

    Adds an element to the beginning of the list.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @param value    The value to add to the list.
    @param-type     T
    @linkage        extern
    @return-type    bool
    |#
    (def push-front
      (fn extern bool ((lst (ref (uq typenode))) (value (uq T)))
        (let ((new-node \ (malloc' 1 (uq nodetypenode))))
          (setf (:@ new-node element) value)
          (setf (:@ new-node next) (nullptr (uq nodetypenode)))
          (setf (:@ new-node prev) (nullptr (uq nodetypenode)))
          (and (null (@:@ lst first))
               (do (setf (:@ lst first) new-node)
                   (setf (:@ lst last)  new-node)
                   (return true)))
          (let ((first-node \ (@:@ lst first)))
            (setf (:@ first-node prev) new-node)
            (setf (:@ new-node  next) first-node)
            (setf (:@ lst first) new-node)))
        true))

    #|
    @fn pop-front

    Removes an element from the beginning of the list.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    void
    |#
    (def pop-front
      (fn extern void ((lst (ref (uq typenode))))
        (and (empty (@ lst)) (return))
        (let ((first-node  \ (@:@ lst first))
              (second-node \ (@:@ first-node next))
              (size        \ (size (@ lst))))
          (if (null second-node)
              (do (setf (:@ lst first) (nullptr (uq nodetypenode)))
                  (setf (:@ lst last)  (nullptr (uq nodetypenode))))
              (do (setf (:@ second-node prev)
                        (nullptr (uq nodetypenode)))
                  (setf (:@ lst first) second-node)))
          (let ((value \ (:@ first-node element)))
            (destroy value))
            (free' first-node))
        (return)))

    #|
    @struct (Iterator (List T))

    @linkage extern
    |#
    (def (Iterator (List (uq T)))
      (struct extern ((node (p (uq nodetypenode))))))

    #|
    @fn begin

    Returns the iterator for the first list element.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    (Iterator (List T))
    |#
    (def begin
      (fn extern (Iterator (List (uq T))) ((lst (ref (uq typenode))))
        ((Iterator (List (uq T))) ((node (@:@ lst first))))))

    #|
    @fn end

    Returns the iterator representing the end of the list (sentinel).

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    (Iterator (List T))
    |#
    (def end
      (fn extern (Iterator (List (uq T))) ((lst (ref (uq typenode))))
        ((Iterator (List (uq T))) ((node (nullptr (uq nodetypenode)))))))

    #|
    @macro value-type

    Expands to the underlying value type (i.e. `T`) of the iterator.

    @param iter     A type node.
    @param-type     (p (Iterator (List T)))
    @linkage        extern
    |#
    (def value-type
      (macro extern ((iter (p (Iterator (List (uq T))))))
        (qq do (uq T))))

    #|
    @macro distance-type

    Expands to the underlying distance type of the iterator.

    @param iter     A type node.
    @param-type     (p (Iterator (List T)))
    @linkage        extern
    |#
    (def distance-type
      (macro extern ((iter (p (Iterator (List (uq T))))))
        (qq do size)))

    #|
    @fn @source

    Returns the iterator's value.

    @param iter     An iterator.
    @param-type     (Iterator (List T))
    @linkage        extern
    @return-type    T
    |#
    (def @source
      (fn extern (uq T) ((iter (Iterator (List (uq T)))))
        (@:@ (@: iter node) element)))

    #|
    @fn source

    Returns a pointer to the iterator's value.

    @param iter     An iterator.
    @param-type     (Iterator (List T))
    @linkage        extern
    @return-type    (p T)
    |#
    (def source
      (fn extern (p (uq T)) ((iter (Iterator (List (uq T)))))
        (:@ (@: iter node) element)))

    #|
    @fn sink

    Set the given value at the specified position in the list.

    @param iter     An iterator.
    @param-type     (Iterator (List T))
    @param v        The new value.
    @param-type     T
    @return-type    bool
    |#
    (def sink
      (fn extern bool ((iter (Iterator (List (uq T))))
                       (v (uq T)))
        (setf (:@ (@: iter node) element) v)))

    #|
    @fn successor

    Returns the iterator for the position that follows the argument
    iterator.

    @param iter     An iterator.
    @param-type     (Iterator (List T))
    @linkage        extern
    @return-type    (Iterator (List T))
    |#
    (def successor
      (fn extern (Iterator (List (uq T))) ((iter (Iterator (List (uq T)))))
        ((Iterator (List (uq T))) ((node (@:@ (@: iter node) next))))))

    #|
    @fn predecessor

    Returns the iterator for the position just before the argument
    iterator.

    @param iter     An iterator.
    @param-type     (Iterator (List T))
    @linkage        extern
    @return-type    (Iterator (List T))
    |#
    (def predecessor
      (fn extern (Iterator (List (uq T))) ((iter (Iterator (List (uq T)))))
        ((Iterator (List (uq T))) ((node (@:@ (@: iter node) prev))))))

    #|
    @fn =

    @param iter1    The first iterator.
    @param-type     (Iterator (List T))
    @param iter2    The second iterator.
    @param-type     (Iterator (List T))
    @linkage        extern
    @return-type    bool
    |#
    (def =
      (fn extern bool ((iter1 (Iterator (List (uq T))))
                       (iter2 (Iterator (List (uq T)))))
        (p= (@: iter1 node)
            (@: iter2 node))))

    (implement Type (Iterator (List (uq T))))
    (instantiate != (Iterator (List (uq T))))

    #|
    @struct (ReverseIterator (List T))

    @linkage extern
    |#
    (def (ReverseIterator (List (uq T)))
      (struct extern ((node (p (uq nodetypenode))))))

    #|
    @fn rbegin

    Returns the iterator for the last list element.

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    (ReverseIterator (List T))
    |#
    (def rbegin
      (fn extern (ReverseIterator (List (uq T))) ((lst (ref (uq typenode))))
        ((ReverseIterator (List (uq T))) 
         ((node (@:@ lst last))))))

    #|
    @fn rend

    Returns the iterator representing the beginning of the list (sentinel).

    @param lst      A list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    (ReverseIterator (List T))
    |#
    (def rend
      (fn extern (ReverseIterator (List (uq T))) ((lst (ref (uq typenode))))
        ((ReverseIterator (List (uq T))) 
         ((node (nullptr (uq nodetypenode)))))))

    (def value-type
      (macro extern ((lstiter (p (ReverseIterator (List (uq T))))))
        (qq do (uq T))))

    (def distance-type
      (macro extern ((veciter (p (ReverseIterator (List (uq T))))))
        (qq do size)))

    (def @source
      (fn extern (uq T) ((iter (ReverseIterator (List (uq T)))))
        (@:@ (@: iter node) element)))

    (def source
      (fn extern (p (uq T)) ((iter (ReverseIterator (List (uq T)))))
        (:@ (@: iter node) element)))

    (def sink
      (fn extern bool ((iter (ReverseIterator (List (uq T))))
                       (sinker (uq T)))
        (setf (:@ (@: iter node) element) sinker)
        true))

    (def successor
      (fn extern (ReverseIterator (List (uq T))) 
                    ((iter (ReverseIterator (List (uq T)))))
        ((ReverseIterator (List (uq T))) 
          ((node (@:@ (@: iter node) prev))))))

    (def predecessor
      (fn extern (ReverseIterator (List (uq T))) 
                    ((iter (ReverseIterator (List (uq T)))))
        ((ReverseIterator (List (uq T))) 
          ((node (@:@ (@: iter node) next))))))

    (def =
      (fn extern bool ((iter1 (ReverseIterator (List (uq T))))
                       (iter2 (ReverseIterator (List (uq T)))))
        (p= (@: iter1 node)
            (@: iter2 node))))

    (implement Type (ReverseIterator (List (uq T))))
    (instantiate != (ReverseIterator (List (uq T))))

    #|
    @fn insert

    Inserts a new element into the list before the specified position,
    and returns the iterator for the new element.

    @param lst      The list pointer.
    @param-type     (p (List T))
    @param iter     The iterator for the position.
    @param-type     (Iterator (List T))
    @param value    The new value.
    @param-type     T
    @linkage        extern
    @return-type    (Iterator (List T))
    |#
    (def insert
      (fn extern (Iterator (List (uq T))) 
                    ((lst   (ref (uq typenode)))
                     (iter  (Iterator (List (uq T))))
                     (value (uq T)))
        (and (empty (@ lst))
             (do (push-back (@ lst) value)
                 (return (begin (@ lst)))))
        (let ((new-node \ (malloc' 1 (uq nodetypenode))))
          (setf (:@ new-node element) value)
          (setf (:@ new-node next) (@: iter node))
          (setf (:@ new-node prev) (@:@ (@: iter node) prev))
          (setf (:@ (@:@ (@: iter node) prev) next) new-node)
          (setf (:@ (@: iter node) prev) new-node)
          (return iter))))

    #|
    @fn erase

    Remove the element at the specified position from the list.

    @param lst      The list pointer.
    @param-type     (p (List T))
    @param iter     The iterator for the position.
    @param-type     (Iterator (List T))
    @linkage        extern
    @return-type    (Iterator (List T))
    |#
    (def erase
      (fn extern (Iterator (List (uq T))) 
                    ((lst   (ref (uq typenode)))
                     (iter  (Iterator (List (uq T)))))
        (and (empty (@ lst))
             (return iter))
        (let ((node      \ (@: iter node))
              (prev-node \ (@:@ node prev))
              (next-node \ (@:@ node next)))
          (if (not (null prev-node))
              (setf (:@ prev-node next) next-node)
              (setf (:@ lst first) next-node))
          (if (not (null next-node))
              (setf (:@ next-node prev) prev-node)
              (setf (:@ lst last) prev-node))
          (let ((value \ (:@ node element)))
            (destroy value))
            (free' node))
        iter))

    #|
    @fn clear

    Remove all of the elements from the list.

    @param lst      The list pointer.
    @param-type     (p (List T))
    @linkage        extern
    @return-type    bool
    |#
    (def clear
      (fn extern bool ((lst (ref (uq typenode))))
        (while (not (empty (@ lst)))
          (pop-front (@ lst)))
        true))

    #|
    @fn swap

    @param lst1     The first list pointer.
    @param lst2     The second list pointer.
    @param-type     (p (List T))
    @param-type     (p (List T))
    @linkage        extern
    @return-type    void
    |#
    (def swap
      (fn extern void ((lst1 (ref (uq typenode)))
                       (lst2 (ref (uq typenode))))
        (let ((first-1 \ (@:@ lst1 first))
              (last-1  \ (@:@ lst1 last))
              (first-2 \ (@:@ lst2 first))
              (last-2  \ (@:@ lst2 last)))
          (setf (:@ lst1 first) first-2)
          (setf (:@ lst1 last)  last-2)
          (setf (:@ lst2 first) first-2)
          (setf (:@ lst2 last)  last-2)
          (return))))

    (def setf-copy (fn extern bool ((dst (p (uq typenode)))
                                    (src (p (uq typenode))))
      (init (@ dst))
      (let ((b1 \ (begin (@ src)))
            (e1 \ (end   (@ src)))
            (b2 \ (begin (@ dst)))
            (e2 \ (end   (@ dst))))
        (for true (!= b1 e1) (do (setv b1 (successor b1))
                                 (setv b2 (successor b2)))
          (sink b2 (@source b1))))
      true))

    (def setf-assign (fn extern bool ((dst (p (uq typenode)))
                                      (src (p (uq typenode))))
      (clear (@ dst))
      (setf-copy dst src)))

    (implement Container (uq typenode))
    (implement Type (uq typenode))
    (mfor F (= != < <= > >=)
      (instantiate F (uq typenode)))

    (implement EqualityComparable (uq typenode))
    (implement LessThanComparable (uq typenode))

    (def destroy
      (fn extern void ((lstp (p (uq typenode))))
        (clear (@ lstp))
        (return)))

    (instantiate swap (Iterator (List (uq T))))
    (instantiate swap (ReverseIterator (List (uq T))))
    (implement OutputIterator (Iterator (List (uq T))))
    (implement OutputIterator (ReverseIterator (List (uq T))))
    (implement BidirectionalIterator (Iterator (List (uq T))))
    (implement BidirectionalIterator (ReverseIterator (List (uq T))))
    (implement FrontInsertionSequence (uq typenode))
    (implement BackInsertionSequence (uq typenode))
    (implement ReversibleContainer (uq typenode))))
)))))
